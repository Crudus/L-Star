
 1 REM PROGRAM INIT
 10 PRINT "TIC-TAC-TOE FOR KIM-1 / MICROKIM"
 15 PRINT "(C) 2010 JAC GOUDSMIT. LICENSE: MIT"
 20 DIM A(2,2):DIM C(2,7)
 30 L=0:REM INTELLIGENCE LEVEL
 50 REM INIT BOARD
 60 FOR I=0 TO 2:FOR J=0 TO 2:A(I,J)=0:NEXT J:NEXT I
 70 GOSUB 4000
 80 GOSUB 1000
 100 REM MAIN LOOP
 110 GOSUB 2000:IF N=9 THEN 200
 120 IF N=0 THEN 150:REM COMPUTER MAKES FIRST MOVE
 130 GOSUB 1000
 140 GOSUB 4000:IF N=9 THEN 200
 150 GOSUB 3000:IF N=9 THEN 200
 160 GOSUB 1000
 170 GOSUB 4000:IF N=9 THEN 200
 180 GOTO 100
 200 REM GAME OVER
 210 PRINT "PLAY AGAIN ";
 220 GOSUB 300
 230 IF Y THEN 50
 240 PRINT "ALRIGHT..."
 250 END
 300 REM YES OR NO
 310 PRINT "(Y/N)";
 320 INPUT Y$
 330 Y$=LEFT$(Y$,1)
 340 Y=Y$="Y"
 350 IF Y OR Y$="N" THEN RETURN
 360 PRINT "PLEASE ANSWER Y OR N"
 370 GOTO 300
 1000 REM DRAW BOARD
 1020 FOR I=0 TO 2
 1030 FOR J=0 TO 2
 1040 ON A(I,J)+1 GOTO 1050,1060,1070
 1050 PRINT I*3+J+1;:GOTO 1080
 1060 PRINT " X ";:GOTO 1080
 1070 PRINT " O ";:GOTO 1080
 1080 NEXT J
 1090 PRINT
 1100 NEXT I
 1110 RETURN
 2000 REM USER MOVES
 2010 PRINT "ENTER YOUR MOVE"
 2020 IF N=0 THEN PRINT "0=LET COMPUTER MAKE FIRST MOVE":GOTO 2040
 2030 PRINT "0=GIVE UP"
 2040 INPUT M
 2050 IF M=0 AND N=0 THEN RETURN: REM LET COMPUTER DO FIRST MOVE
 2060 IF M>9 THEN PRINT "PLEASE ENTER A NUMBER 0-9":GOTO 2000
 2070 IF M<>0 THEN 2100
 2080 PRINT "GIVE UP ";:GOSUB 300:IF Y THEN N=9:RETURN
 2090 GOTO 2000
 2100 M=M-1:Y=INT(M/3):X=M-3*Y
 2110 IF A(Y,X)<>0 THEN PRINT "THAT SQUARE IS TAKEN":GOTO 2000
 2120 A(Y,X)=2
 2130 RETURN
 3000 REM COMPUTER MOVES
 3010 M=-1
 3020 REM FIND ANY ROW/COL WHERE WE HAVE 2 IN A ROW
 3025 IF L=0 THEN 3080:REM NOT SMART ENOUGH TO SEE COMPUTER CAN WIN
 3030 FOR I=0 TO 7
 3040 IF C(1,I)=2 AND C(0,I)<>0 THEN M=I:I=7
 3050 NEXT I
 3070 IF M>=0 THEN 3900
 3080 REM FIND ANY ROW/COL WHERE USER HAS 2 IN A ROW
 3085 IF L<=1 THEN 3130:REM NOT SMART ENOUGH TO KEEP USER FROM WINNING
 3090 FOR I=0 TO 7
 3100 IF C(2,I)=2 AND C(0,I)<>0 THEN M=I:I=7
 3110 NEXT I
 3120 IF M>=0 THEN 3900
 3130 REM GIVE UP IF WE CAN'T WIN I.E. USER IS IN ALL ROW/COL
 3135 IF L<=2 THEN 3180:REM NOT SMART ENOUGH TO GIVE UP
 3140 FOR I=0 TO 7
 3150 IF C(2,I)=0 THEN M=I:I=7
 3160 NEXT I
 3170 IF M<0 THEN PRINT "COMPUTER GIVES UP!":N=9:RETURN
 3180 REM ON FIRST MOVE, PREFER THE CENTER SQUARE OR A CORNER
 3185 IF N>1 OR L<=3 THEN 3200:REM NOT SMART ENOUGH TO HAVE A STRATEGY
 3190 M=INT(3*RND(1))-1:IF M<0 THEN 3200
 3195 M=M+6:GOTO 3900
 3200 REM ON SECOND MOVE, ATTEMPT TO ACHIEVE SYMMETRY
 3205 IF N<=1 OR N>3 OR L<=4 THEN 3250
 3210 M=INT(3*RND(1))-1:IF M<0 THEN 3250
 3220 M=-1:FOR I=6 TO 7
 3230 IF C(1,I)>0 AND C(2,I)=0 THEN M=I:I=7
 3240 NEXT
 3250 REM
 3880 REM NO PREFERENCE YET, JUST GENERATE A RANDOM MOVE
 3890 M=INT(8*RND(1))
 3895 IF C(0,M)=0 THEN 3890:REM KEEP TRYING (YES I KNOW, YUCK)
 3900 REM GENERATE RANDOM MOVE IN ROW/COLUMN M
 3910 ON M+1 GOTO 3920,3920,3920,3940,3940,3940,3960,3960
 3920 REM ROW M
 3930 X=INT(3*RND(1)):Y=M:GOTO 3980
 3940 REM COLUMN M-3
 3950 X=M-3:Y=INT(3*RND(1)):GOTO 3980
 3960 REM DIAGONALS
 3970 X=INT(3*RND(1)):Y=((7-M)*X)+((M-6)*(2-X))
 3980 IF A(Y,X)<>0 THEN 3910:REM KEEP TRYING (YES I KNOW, YUCK)
 3990 PRINT "COMPUTER MOVES";1+Y*3+X
 3995 A(Y,X)=1
 3999 RETURN
 4000 REM COUNT BLANKS, X'S AND O'S, CHECK FOR GAME OVER
 4010 N=0:FOR I=0 TO 2:FOR J=0 TO 7:C(I,J)=0:NEXT J:NEXT I
 4020 FOR I=0 TO 2
 4030 FOR J=0 TO 2
 4040 V=A(I,J):IF V THEN N=N+1
 4050 C(V,I)=C(V,I)+1: REM INCREASE ROW COUNTER
 4060 C(V,3+J)=C(V,3+J)+1: REM INCREASE COLUMN COUNTER
 4070 NEXT J
 4080 V=A(I,I):C(V,6)=C(V,6)+1: REM INCREASE 1ST DIAGONAL COUNTER
 4090 V=A(2-I,I):C(V,7)=C(V,7)+1: REM INCREASE 2ND DIAG COUNTER
 4100 NEXT I
 4101 REM AT THIS POINT, N HOLDS TOTAL COUNT OF NON-BLANKS
 4102 REM AND C(V,RCD) CONTAINS COUNTS FOR EACH VALUE V
 4103 REM AND FOR EACH ROW RCD[0..2], COLUMN[3..5] AND
 4104 REM DIAGONAL[6..7]
 4110 IF N=9 THEN PRINT "IT'S A TIE!":RETURN
 4120 FOR I=0 TO 7
 4130 FOR J=1 TO 2
 4140 IF C(J,I)<3 THEN 4180
 4145 IF J=1 AND L>0 THEN L=L-1:REM LESS SMART IF COMPUTER WINS
 4146 IF J=2 AND L<9 THEN L=L+1:REM SMARTER IF USER WINS
 4150 IF J=1 THEN PRINT "COMPUTER WINS!":GOTO 4170
 4160 PRINT "YOU WIN!"
 4170 N=9:J=2:I=7
 4180 NEXT J
 4190 NEXT I
 4200 REM AT THIS POINT, N=9 INDICATES GAME OVER
 4210 RETURN

